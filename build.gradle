buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.0"
    }
}

plugins {
    id "java" // or "groovy" Must be explicitly applied
    id "idea"
    id "com.github.johnrengelman.shadow" version "1.2.0"
}

version = "1.0"
jar {
    manifest {
        attributes "Main-Class": "com.thathurleyguy.CasseroleApplication"
        attributes "Built-Date": new Date() //now
        attributes "Built-By": System.getProperty("user.name")
        attributes "Build-Jdk": System.getProperty("java.version")
        attributes "Implementation-Title": project.name
        attributes "Implementation-Version": project.version
        attributes "Implementation-Vendor-Id": project.group
    }
}

project.ext {
    dropwizardVersion = "0.7.1"
}

repositories {
    mavenCentral()
}

dependencies {
    compile (
            "io.dropwizard:dropwizard-core:" + dropwizardVersion,
            "com.datastax.cassandra:cassandra-driver-core:2.1.4"
    )
    testCompile group: "junit", name: "junit", version: "4.11"
}

// Configure the run task to start the Dropwizard service
task run(dependsOn: "shadowJar", group: "dropwizard", description: "start the server") << {
    javaexec {
        main = "-jar"
        args = ["${shadowJar.archivePath}", "server", "./src/dist/config/casserole.yml"]
    }
}

apply from: "gradletasks/db.gradle"
